# Maintainer: Alastair Hughes <hobbitalastair@gmail.com>
pkgname=vostro1510
pkgver=0.1.6
pkgrel=1
pkgdesc="Vostro 1510-specific configuration"
arch=('any')
license=('GPL')
# Install needed packages
depends=('powersave' 'triggerhappy-git' 'xorg-xrandr' 'ash-desktop'
        'wpa_supplicant' 'intel-ucode' 'acpi' 'powertop' 'cdrtools' 'syslinux'
        )

# Files to add
install=install.sh
source=('multimedia.conf' # Triggerhappy multimedia key config
        'video.conf'      # Triggerhappy video switch config
        'suspend.conf'    # Triggerhappy suspend config
        'switchvideo.sh'  # Video switcher script.
        '99-backlight.rules' # Udev rules for backlight
        '99-vga.rules'    # Udev rules for vga input
        'logind.conf'     # Don't suspend on lid switch
       )
md5sums=('42f34d91cc13b690f905d9013a2c8e5f'
         '05021a36bf42099238fafaa669dbf3d9'
         '2a1f76aee7572192189139f943d4734e'
         '70036dc4adb2e7d7d11011fe38f0eb91'
         'f3c0870e81f88af8792b5deea9024415'
         '0b66e4b122d2162e9b7d91753fd0a08a'
         'd96b810d39d5fbc33ee0522601b40fd5')
backup=("etc/triggerhappy/multimedia.conf"
        "etc/triggerhappy/video.conf"
        "etc/triggerhappy/suspend.conf")

package() {
    cd "${srcdir}"

    # Add the triggerhappy configs.
    for f in multimedia video suspend; do
        install -Dm0644 "${srcdir}/${f}.conf" \
            "${pkgdir}/etc/triggerhappy/${f}.conf"
    done

    # Add the video switcher script.
    install -Dm0755 "${srcdir}/switchvideo.sh" \
        "${pkgdir}/usr/bin/switchvideo"

    # Enable the services
    SERVICES="triggerhappy"
    mkdir -p "${pkgdir}/etc/systemd/system/multi-user.target.wants"
    for SERVICE in $SERVICES; do
        ln -s "/usr/lib/systemd/system/${SERVICE}.service" \
      "${pkgdir}/etc/systemd/system/multi-user.target.wants/${SERVICE}.service"
    done

    # Re-add the dhcpcd hook for wpa_supplicant.
    mkdir -p "${pkgdir}/usr/lib/dhcpcd/dhcpcd-hooks"
    ln -s "/usr/share/dhcpcd/hooks/10-wpa_supplicant" \
        "${pkgdir}/usr/lib/dhcpcd/dhcpcd-hooks/10-wpa_supplicant"

    # Fix the backlight.
    install -Dm0644 "99-backlight.rules" \
        "${pkgdir}/usr/lib/udev/rules.d/99-backlight.rules"

    # Add the VGA switch detection rule.
    install -Dm0644 "99-vga.rules" \
        "${pkgdir}/usr/lib/udev/rules.d/99-vga.rules"

    # Add the patch for the lid switch.
    mkdir -p "${pkgdir}/var/lib/patchman/etc/systemd/"
    install -Dm0644 "logind.conf" \
        "${pkgdir}/var/lib/patchman/etc/systemd/logind.conf/lid.patch"
}
