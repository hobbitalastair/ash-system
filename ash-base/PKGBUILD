# Maintainer: Alastair Hughes <hobbitalastair@gmail.com>
pkgname=ash-base
pkgver=0.8.1
pkgrel=1
pkgdesc="Base setup for an Alastair Hughes system"
arch=('any')
license=('MIT')
depends=(
         'patchman'

         'vim'
         'less'
         'sudo'
         'bash-completion'
         'dhcpcd'
         'man-db'
         'man-pages'
         'unzip'
         'usbutils'
         'inetutils'   # for hostname
         'pacman-contrib'
         'which'
         'patch'

         'fscrypt'
         'pass'
         'pam-gnupg'

         'openssh'
         'tarsnap'
        )
install="install.sh"
source=(
        'bash.bashrc'
        'hosts'
        'sshd_config.patch'
        'vim.conf'
        'terminal.sh'
        'sudoers.ash'
        'startup.py'
        'size.conf'
        'backup.service'
        'backup.timer'
        'pam_system_login.patch'
        'pam_passwd.patch'
        'pam_env.conf'
       )
md5sums=('29bcdc481ca6cffd349b83dea9eb88f8'
         '9da6dec483235ccf81b91e386f49c8c5'
         '3d111c66c4fb73ecdf70ce03aafba195'
         '03842a51f2855af2392efa52d75def26'
         '7f84c64499e064dba72276a8e09a1b91'
         '8fbffcfe9361a2bb838c670306f5b36f'
         'ac538ff79175e897ed821f4cd49d5d74'
         '372f1f188276ac5881f315169e3c4e12'
         '2ab318f27d4fb2e7f5311d3d5943e602'
         '761c19166a0e2e17f146ee95506f009e'
         'cfa9a9df080e9170240134a1e2b5382b'
         '0a7d8310c4aa31c37a7a1c043e43601b'
         '007d320b689eb942575d4dafbb824df2')

package() {
    cd "${srcdir}"

    # Install the default patches - these need to be applied manually.
    patchpath="${pkgdir}/var/lib/patchman"
    install -Dm0644 "bash.bashrc" "${patchpath}/etc/bash.bashrc"
    install -Dm0644 "hosts" "${patchpath}/etc/hosts"
    install -Dm0644 "sshd_config.patch" \
        "${patchpath}/etc/ssh/sshd_config/auth.patch"
    install -Dm0644 "pam_system_login.patch" \
        "${patchpath}/etc/pam.d/system-login/auth.patch"
    install -Dm0644 "pam_passwd.patch" \
        "${patchpath}/etc/pam.d/passwd/fscrypt.patch"
    install -Dm0644 "pam_env.conf" \
        "${patchpath}/etc/security/pam_env.conf"

    # Add the IGNORE flags for some files.
    for f in etc/fstab etc/passwd etc/shadow etc/group etc/gshadow \
        etc/locale.gen etc/resolv.conf etc/shells; do
        mkdir -p "${patchpath}/${f}"
        touch "${patchpath}/${f}/IGNORE"
    done

    # Add the new files.
    install -Dm0644 "vim.conf" "${pkgdir}/etc/vim.conf"
    install -Dm0755 "terminal.sh" \
                    "${pkgdir}/etc/profile.d/terminal.sh"
    install -dm0750 "${pkgdir}/etc/sudoers.d"
    install -Dm0440 "sudoers.ash" "${pkgdir}/etc/sudoers.d/ash-base"
    install -Dm0644 "startup.py" "${pkgdir}/etc/python/startup.py"
    install -Dm0644 "size.conf" \
        "${pkgdir}/etc/systemd/journald.conf.d/size.conf"

    # Add the skel files
    mkdir -p "${pkgdir}/etc/skel/.config"
    mkdir -p "${pkgdir}/etc/skel/.local/share/vim"

    # Add the user services.
    install -Dm0644 "backup.service" \
        "${pkgdir}/usr/lib/systemd/user/backup.service"
    install -Dm0644 "backup.timer" \
        "${pkgdir}/usr/lib/systemd/user/backup.timer"

    # Enable the services
    SERVICES="dhcpcd systemd-timesyncd"
    mkdir -p "${pkgdir}/etc/systemd/system/multi-user.target.wants"
    for SERVICE in $SERVICES; do
        ln -s "/usr/lib/systemd/system/${SERVICE}.service" \
      "${pkgdir}/etc/systemd/system/multi-user.target.wants/${SERVICE}.service"
    done
}
