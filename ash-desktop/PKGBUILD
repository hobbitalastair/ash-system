# Maintainer: Alastair Hughes <hobbitalastair@gmail.com>
pkgname=ash-desktop
pkgver=1.5
pkgrel=1
pkgdesc="Basic desktop setup for an Alastair Hughes system"
arch=('any')
license=('GPL')
# Install needed packages
install=install.sh
depends=('chromium' 'gnome-themes-standard' 'oxygen-icons' 'pepper-flash'
        'dwm' 'xssstate' 'slock' 'slstatus' 'redshift'
        'scrot'
        'xorg-server' 'xorg-xinit' 'xclip'
        'cups' 'ghostscript' 'gtk3-print-backends'
        'alsa-utils' 'vorbis-tools'
        'feedutils' 'webutils'
        'teapot'
        'gvim' # for clipboard integration
        'hunspell' 'hunspell-en_AU'
        'texlive-core' 'texlive-moderncv'
        )
source=('gtkrc-2'
        'desktop.sh'
        'slock@.service'
        'slock-xorg.conf'
        'multilib.patch'
        )
md5sums=('050d0a2ed0ecb66bea06db9692bfc549'
         'a58f90c5db213979878b0663eb61e2a7'
         '1dcb7fe86db8c8e5a7b7b9c181ffc5fc'
         '12bc8dbe762383f5b785720f6ed3044c'
         '89de1bcdd219f982b8445c80520d92b3')

package() {
    cd "${srcdir}"

    # Global GTK config
    install -Dm0644 "${srcdir}/gtkrc-2" "${pkgdir}/etc/gtk-2.0/gtkrc"

    # Desktop profile.
    install -Dm0644 "${srcdir}/desktop.sh" "${pkgdir}/etc/profile.d/desktop.sh"

    # Slock service.
    install -Dm0644 "slock@.service" \
        "${pkgdir}/usr/lib/systemd/system/slock@.service"
    # Enable the service for ash.
    mkdir -p "${pkgdir}/etc/systemd/system/sleep.target.wants"
    ln -s "/usr/lib/systemd/system/slock@.service" \
      "${pkgdir}/etc/systemd/system/sleep.target.wants/slock@ash.service"
    # Stop the user from killing the x server.
    install -Dm0644 "slock-xorg.conf" \
        "${pkgdir}/usr/share/X11/xorg.conf.d/50-slock.conf"

    # Enable multilib
    patchpath="${pkgdir}/var/lib/patchman"
    install -Dm0644 "multilib.patch" \
        "${patchpath}/etc/pacman.conf/multilib.patch"
}
