# Maintainer: Alastair Hughes <hobbitalastair@gmail.com>
pkgname=ash-arch
pkgver=0.6
pkgrel=1
pkgdesc="Base setup for an Alastair Hughes Arch-based system"
arch=('any')
license=('MIT')
depends=(
         'ash-base'
         'systemd'
        )
optdepends=(
    'tarsnap: for backups'
    'unison-248-compat: for sync'
)
source=(
        'size.conf'
        'sync@.service'
        'sync@.timer'
        'backup.service'
        'backup.timer'
       )
md5sums=('372f1f188276ac5881f315169e3c4e12'
         '52bc8e5123601c84c7374ee162624332'
         'bcfaf3be811f380f4f558e245c05ab75'
         '2ab318f27d4fb2e7f5311d3d5943e602'
         '761c19166a0e2e17f146ee95506f009e')

package() {
    cd "${srcdir}"

    # Add the new files.
    install -Dm0644 "size.conf" \
        "${pkgdir}/etc/systemd/journald.conf.d/size.conf"

    # Add the skel files
    mkdir -p "${pkgdir}/etc/skel/.config/unison"
    mkdir -p "${pkgdir}/etc/skel/.local/share/unison/backups"

    # Add the user services.
    install -Dm0644 "sync@.service" \
        "${pkgdir}/usr/lib/systemd/user/sync@.service"
    install -Dm0644 "sync@.timer" "${pkgdir}/usr/lib/systemd/user/sync@.timer"
    install -Dm0644 "backup.service" \
        "${pkgdir}/usr/lib/systemd/user/backup.service"
    install -Dm0644 "backup.timer" "${pkgdir}/usr/lib/systemd/user/backup.timer"

    # Enable the services
    SERVICES="sshd dhcpcd systemd-timesyncd"
    mkdir -p "${pkgdir}/etc/systemd/system/multi-user.target.wants"
    for SERVICE in $SERVICES; do
        ln -s "/usr/lib/systemd/system/${SERVICE}.service" \
      "${pkgdir}/etc/systemd/system/multi-user.target.wants/${SERVICE}.service"
    done
}
